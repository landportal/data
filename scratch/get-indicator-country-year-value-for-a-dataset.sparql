PREFIX cex: <http://purl.org/weso/ontology/computex#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX time: <http://www.w3.org/2006/time#>

SELECT ?indicatorID ?countryISO3 (year(?dateTime) AS ?year) ?value
FROM <http://data.landportal.info>
WHERE {

?obs cex:ref-indicator ?indicatorURL ;
     cex:ref-area ?countryURL ;
     cex:value ?value ;
     cex:ref-time ?time .

     ?time time:hasBeginning ?timeValue .
     ?timeValue time:inXSDDateTime ?dateTime .

     ?indicatorURL dct:source ?datasetURL. 

BIND (REPLACE(STR(?countryURL), "http://data.landportal.info/geo/","") AS ?countryISO3)
BIND (REPLACE(STR(?indicatorURL), "http://data.landportal.info/indicator/","") AS ?indicatorID)

VALUES ?datasetURL {<http://data.landportal.info/dataset/RRI-FTD>}

}
