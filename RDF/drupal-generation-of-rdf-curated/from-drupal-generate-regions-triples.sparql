# Select only the ones with ISO3 (3 letters)
# As far as UN M49 is also skos:notation

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geonames: <http://www.geonames.org/ontology#>

CONSTRUCT   {
   ?regionURI a <http://purl.org/weso/landbook/ontology#Country> ;
              rdfs:label ?label ;
              dct:title ?label ;
              dct:identifier ?iso3 ;
              skos:notation ?iso3 ;
              geonames:countryCode ?iso3 .
}
FROM <http://regions.landportal.info>
WHERE {
   ?region a <http://schema.org/Place> ;
           skos:notation ?iso3 ;
           rdfs:label ?label .
   FILTER ((REGEX(?iso3,"[A-Za-z]")) && (STRLEN(?iso3) = 3)) .
   BIND (URI(CONCAT("http://data.landportal.info/geo/",?iso3)) AS ?regionURI)
} ORDER BY (?iso3)