# Only in the graph http://indicators.landportal.info , the indicator has the skos:prefLabel 

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX sdmx-attribute: <http://purl.org/linked-data/sdmx/2009/attribute#>

CONSTRUCT {
   ?indicatorURI  a <http://purl.org/weso/ontology/computex#Indicator> ;
                  dct:identifier ?notationIndicator ;
                  skos:notation ?notationIndicator ;
                  rdfs:label  ?label ;
                  dct:title  ?label ;
                  rdfs:comment ?description ;
                  dct:description ?description ;
                  sdmx-attribute:unitMeasure ?unit ;
                  dct:source ?datasetURI ;
                  rdfs:seeAlso ?indicatorDrupalCurated .
}

FROM <http://indicators.landportal.info>
FROM <http://datasets.landportal.info>
WHERE {
?indicatorDrupal a <http://purl.org/weso/ontology/computex#Indicator> ;
                 skos:notation ?notationIndicator ;
                 skos:prefLabel ?label ;
                 skos:definition ?description ;
				 sdmx-attribute:unitMeasure ?unit ;
				 dct:source ?dataset . 

?dataset a qb:DataSet ;
         skos:notation ?notationDataset .

#VALUES ?notationDataset {"NKT-VGGT16"} .

BIND (IRI(CONCAT("http://data.landportal.info/indicator/",?notationIndicator)) AS ?indicatorURI)
BIND (IRI(CONCAT("http://data.landportal.info/dataset/",?notationDataset)) AS ?datasetURI)
BIND (IRI(REPLACE(REPLACE(STR(?indicatorDrupal), "https://","http://"),"beta.landportal.info","landportal.info")) AS ?indicatorDrupalCurated)

}
