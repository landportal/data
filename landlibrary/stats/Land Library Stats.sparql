# Get the number of LLRs in the LOD
PREFIX bibo:<http://purl.org/ontology/bibo/>
PREFIX schema:<http://schema.org/>
SELECT COUNT (DISTINCT ?llr)
FROM <http://library.landportal.info>
WHERE {
    ?llr a bibo:Document .
}

# Get the number of LLRs by country/region
PREFIX bibo:<http://purl.org/ontology/bibo/>
SELECT STR(?area_title) as ?country  (COUNT(?llr) AS ?no_llr)
FROM <http://library.landportal.info>
FROM <http://countries.landportal.info>
WHERE {
    ?llr a bibo:Document ;
     dct:spatial ?area .
?area dct:title ?area_title .
}
GROUP BY ?area_title
ORDER BY ?area_title



# Get the number of distinct publishers of LLRs in the LOD
PREFIX bibo:<http://purl.org/ontology/bibo/>
PREFIX schema:<http://schema.org/>
SELECT COUNT (DISTINCT ?publisher)
FROM <http://library.landportal.info>
WHERE {
    ?llr a bibo:Document ;
        schema:publisher ?publisher
}

# Get the list of URI and names of distinct publishers of LLRs in the LOD
PREFIX bibo:<http://purl.org/ontology/bibo/>
PREFIX schema:<http://schema.org/>
SELECT DISTINCT ?publisher ?publisherName
FROM <http://library.landportal.info>
FROM <http://organizations.landportal.info>
WHERE {
    ?llr a bibo:Document ;
        schema:publisher ?publisher .
	?publisher schema:name ?publisherName .
}
ORDER BY ?publisherName


# Get the list of URI, names of distinct publishers of LLRs AND the number of LLRs of each publisher in the LOD
PREFIX bibo:<http://purl.org/ontology/bibo/>
PREFIX schema:<http://schema.org/>
SELECT DISTINCT ?publisher ?publisherName COUNT (?llr) as ?numberOfLLRs
FROM <http://library.landportal.info>
FROM <http://organizations.landportal.info>
WHERE {
    ?llr a bibo:Document ;
        schema:publisher ?publisher .
	?publisher schema:name ?publisherName .
}
ORDER BY ?publisherName

# Get the list of names of distinct publishers of LLRs, the types of organization AND the number of LLRs of each publisher in the LOD
PREFIX bibo:<http://purl.org/ontology/bibo/>
PREFIX schema:<http://schema.org/>
SELECT DISTINCT STR(?publisherName) AS ?publisherName (GROUP_CONCAT(DISTINCT ?organizationType;separator=";") AS ?organizationTypes) COUNT (?llr) as ?numberOfLLRs
FROM <http://library.landportal.info>
FROM <http://organizations.landportal.info>
FROM <http://organization-types.landportal.info>
WHERE {
    ?llr a bibo:Document ;
        schema:publisher ?publisher .
	?publisher schema:name ?publisherName .
    ?publisher dct:type ?organizationTypeURI .
    ?organizationTypeURI rdfs:label ?organizationType .
}
ORDER BY DESC (?numberOfLLRs)


# Number of resources per resource type
PREFIX bibo:<http://purl.org/ontology/bibo/>
SELECT STR(?type) as ?type (COUNT(?llr) AS ?no_llr)
FROM <http://library.landportal.info>
WHERE {
    ?llr a bibo:Document ;
     dct:type ?type .
FILTER (strStarts(str(?type),"https://landportal.org/library/resource-type/"))
}

GROUP BY ?type
ORDER BY ?type


# Get the list of names of distinct publishers AND the number of LLRs
PREFIX bibo:<http://purl.org/ontology/bibo/>
PREFIX schema:<http://schema.org/>
SELECT DISTINCT ?publisherName COUNT (?llr) as ?count
FROM <http://library.landportal.info>
FROM <http://organizations.landportal.info>
WHERE {
   ?llr a bibo:Document ;
       schema:publisher ?publisher .
?publisher schema:name ?publisherName .
}
ORDER BY DESC (?count)


# Get number of publishers by organization type
PREFIX bibo:<http://purl.org/ontology/bibo/>
PREFIX schema:<http://schema.org/>
SELECT ?organizationTypeURI STR(?organizationType) AS ?organizationType (COUNT(?organizationType) AS ?num_organizations)
FROM <http://library.landportal.info>
FROM <http://organizations.landportal.info>
FROM <http://organization-types.landportal.info>
WHERE {
    
	{SELECT DISTINCT ?publisher 
	    { ?llr a bibo:Document ;
           schema:publisher ?publisher .}}
	   
    ?publisher dct:type ?organizationTypeURI .
    OPTIONAL {?organizationTypeURI rdfs:label ?organizationType }.
}
GROUP BY ?organizationTypeURI ?organizationType
ORDER BY DESC (?num_organizations)


# Get the number of LLRs by language
PREFIX bibo:<http://purl.org/ontology/bibo/>
SELECT STR(?lang_label) as ?lang  (COUNT(?llr) AS ?num_llr)
FROM <http://library.landportal.info>
FROM <http://languages.landportal.info>
WHERE {
    ?llr a bibo:Document ;
     dct:language ?lang .
    ?lang rdfs:label ?lang_label
}
GROUP BY ?lang_label
ORDER BY DESC (?num_llr)