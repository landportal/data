# Get Land Library Resources from a list of countries

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
SELECT DISTINCT ?llr ?llrLabel ?country ?countryLabel ?date
FROM <http://library.landportal.info>
WHERE {
?llr a dct:BibliographicResource ;
    dct:title ?llrLabel ;
    dct:spatial ?country .

# Filter, removing the ones coming from ODM/ODC/ODT/ODMY/ODV
MINUS {?llr edm:dataProvider <http://data.landportal.info/organization/8e61f777-e9e1-4cbd-9f78-1a643f9a7af2> } #ODM
MINUS {?llr edm:dataProvider <http://data.landportal.info/organization/61b3d714-bf64-4267-b12a-4f7ec12b8405> } #ODC
MINUS {?llr edm:dataProvider <http://data.landportal.info/organization/35ad7f49-d4da-4c46-a38b-6827678d6771> } #ODT
MINUS {?llr edm:dataProvider <http://data.landportal.info/organization/718119ec-9b57-4624-87bd-504b38dda0f5> } #ODMY
MINUS {?llr edm:dataProvider <http://data.landportal.info/organization/06d0a096-dec8-4225-a143-16e9e2efec07> } #ODV

VALUES ?country  { 
  <http://data.landportal.info/geo/KHM>
  <http://data.landportal.info/geo/LAO>
  <http://data.landportal.info/geo/MMR>
  <http://data.landportal.info/geo/THA>
  <http://data.landportal.info/geo/VNM>
}
?country rdfs:label ?countryLabel
OPTIONAL { ?llr dct:issued ?date . }
}
ORDER BY DESC(?date)
LIMIT 10
