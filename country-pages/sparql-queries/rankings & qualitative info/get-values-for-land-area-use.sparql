PREFIX cex: <http://purl.org/weso/ontology/computex#>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX ex: <http://www.example.org/rdf#>

SELECT ?ArableLandPer ?PermanentCropsPer ?PermanentPasturesAndMedowsPer ?ForestLandPer ?OtherPer ?TotalLandHa (year(?maxdateTime) as ?year)
FROM <http://data.landportal.info>
WHERE {

?obs1 cex:ref-indicator <http://data.landportal.info/indicator/FAO-6621-5110> ;
     cex:ref-area ?countryURL ;
     cex:value ?ArableLandHa ;
     cex:ref-time ?time .

?obs2 cex:ref-indicator <http://data.landportal.info/indicator/FAO-6650-5110> ;
     cex:ref-area ?countryURL ;
     cex:value ?PermanentCropsHa ;
     cex:ref-time ?time .

?obs3 cex:ref-indicator <http://data.landportal.info/indicator/FAO-6655-5110> ;
     cex:ref-area ?countryURL ;
     cex:value ?PermanentPasturesAndMedowsHa;
     cex:ref-time ?time .

?obs4 cex:ref-indicator <http://data.landportal.info/indicator/FAO-6661-5110> ;
     cex:ref-area ?countryURL ;
     cex:value ?ForestLandHa;
     cex:ref-time ?time .

?obs5 cex:ref-indicator <http://data.landportal.info/indicator/FAO-6601-5110> ;
     cex:ref-area ?countryURL ;
     cex:value ?TotalLandHa;
     cex:ref-time ?time .

     ?time time:hasBeginning ?timeValue .
     ?timeValue time:inXSDDateTime ?maxdateTime .

BIND ((xsd:double(xsd:float(?ArableLandHa)*100/xsd:float(?TotalLandHa))) AS ?ArableLandPer)
BIND ((xsd:double(xsd:float(?PermanentCropsHa)*100/xsd:float(?TotalLandHa))) AS ?PermanentCropsPer)
BIND ((xsd:double(xsd:float(?PermanentPasturesAndMedowsHa)*100/xsd:float(?TotalLandHa))) AS ?PermanentPasturesAndMedowsPer)
BIND ((xsd:double(xsd:float(?ForestLandHa)*100/xsd:float(?TotalLandHa))) AS ?ForestLandPer)
BIND ((100 - ?ArableLandPer  - ?PermanentCropsPer - ?PermanentPasturesAndMedowsPer - ?ForestLandPer) AS ?OtherPer)


{
# Calculate the latest year that have values for all the indicators
SELECT DISTINCT max(?dateTime) as ?maxdateTime ?countryURL
FROM <http://data.landportal.info>
WHERE{

?obs1 cex:ref-indicator <http://data.landportal.info/indicator/FAO-6621-5110> ;
     cex:ref-area ?countryURL ;
     cex:ref-time ?time .
?obs2 cex:ref-indicator <http://data.landportal.info/indicator/FAO-6650-5110> ;
     cex:ref-area ?countryURL ;
     cex:ref-time ?time .
?obs3 cex:ref-indicator <http://data.landportal.info/indicator/FAO-6655-5110> ;
     cex:ref-area ?countryURL ;
     cex:ref-time ?time .
?obs4 cex:ref-indicator <http://data.landportal.info/indicator/FAO-6661-5110> ;
     cex:ref-area ?countryURL ;
     cex:ref-time ?time .

     ?time time:hasBeginning ?timeValue .
     ?timeValue time:inXSDDateTime ?dateTime .
	 
	 VALUES ?countryURL {<http://data.landportal.info/geo/AND>}
}

}
}