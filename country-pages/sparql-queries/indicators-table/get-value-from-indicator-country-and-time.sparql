PREFIX cex: <http://purl.org/weso/ontology/computex#>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX ex: <http://www.example.org/rdf#>

SELECT ?indicatorLabel ?indicatorDescription ?year ?value ?unitLabel ?datasetURL ?datasetLabel ?sourceOrgURL ?sourceOrgLabel
FROM <http://data.landportal.info>
WHERE {

?obs cex:ref-area ?countryURL ;
     cex:ref-indicator ?indicatorURL ;
     cex:value ?value ;
     cex:ref-time ?time .

?time time:hasBeginning ?timeValue .
     ?timeValue time:inXSDDateTime ?dateTime .

?indicatorURL ex:unit ?unitLabel ;
     ex:dataset ?datasetURL .
				   
?datasetURL ex:label ?datasetLabel ;
     ex:org ?sourceOrgURL .
	
?sourceOrgURL ex:label ?sourceOrgLabel.

?indicatorURL ex:label ?indicatorLabel ;
              ex:description ?indicatorDescription .

BIND (STR(YEAR(?dateTime)) AS ?year)

VALUES ?countryURL { <http://data.landportal.info/geo/VNM> }
VALUES ?indicatorURL { <http://data.landportal.info/indicator/FAO-23045-6083> }
VALUES ?year {'2007'}
}
